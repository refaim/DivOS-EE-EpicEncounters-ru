#TEMPLATE "_SKILL_Base" : Target_ImmuneToElectrifying
	
INIT
CHARACTER:__Me
FLOAT:%SkillTargetScore_Target_ImmuneToElectrifying=-1000000
// [GLOBAL_VARS]
FLOAT:%Target_ImmuneToElectrifying_MyInitiative
// [~GLOBAL_VARS]
	
EVENTS
	
// [EVENTS]
// [~EVENTS]
	
// Calculate the priority of the skill
EVENT CalculateScore_Target_ImmuneToElectrifying
VARS
	// [CALC_VARS]
	FLOAT:_SkillRangeMin
	FLOAT:_SkillRangeMax
	FLOAT3:_TargetPos
	// [~CALC_VARS]
ON
	OnFunction("CalculateScore_Target_ImmuneToElectrifying")
ACTIONS
	Set(%SkillTargetScore_Target_ImmuneToElectrifying, -1000000)

	// [CALC_ACTIONS_PRE]
	// [~CALC_ACTIONS_PRE]
	// [CALC_ACTIONS]
	IF "c1"
		CharacterGetStat(%Target_ImmuneToElectrifying_MyInitiative, __Me, Initiative)
	THEN
		IterateCharactersInCombat(__Me, "CalculateTarget_Target_ImmuneToElectrifying", Lowest, Vitality)
		IF "!c1"
			IsEqual(%SkillTargetChar_Target_ImmuneToElectrifying, null)
		THEN
			Add(%ActionScore_Target_ImmuneToElectrifying, %SkillTargetScore_Target_ImmuneToElectrifying)
		ENDIF
	ENDIF
	// [~CALC_ACTIONS]
	
	
	
// Calculate the character we should target
EVENT CalculateTarget_Target_ImmuneToElectrifying
VARS	
	CHARACTER:_Char
	// [CALC_TARGET_VARS]
	INT:_ClassScore_Target_ImmuneToElectrifying
	FLOAT:_StatResistance
	FLOAT:_Score
	FLOAT:_SkillRangeMin
	FLOAT:_SkillRangeMax
	FLOAT:_DistanceToChar
	FLOAT:_DistanceScore
	FLOAT:_SaveScore
	FLOAT:_Vitality
	FLOAT:_TargetInit
	// [~CALC_TARGET_VARS]
ON
	OnIterateCharacter(_Char, "CalculateTarget_Target_ImmuneToElectrifying")	
ACTIONS
	// [CALC_CHAR_TARGET_ACTIONS_PRE]
	Set(_Score, 0.0)
	// [~CALC_CHAR_TARGET_ACTIONS_PRE]
	// [CALC_CHAR_TARGET_ACTIONS]
	IF "!c1"
		CharacterIsDead(_Char)
	THEN
		IF "c1"
			CharacterIsAlly(__Me, _Char)
		THEN
			IF "!c1"
				CharacterIsSummon(_Char)
			THEN
				IF "!c1"
					CharacterHasStatus(_Char, CHARMED)
				THEN
					IF "c1|c2|c3"
						CharacterHasStatus(_Char, STUNNED)
						CharacterHasStatus(_Char, CONSUME, "AMER_STATUS_Shocked")
						CharacterHasStatus(_Char, WET)
					THEN
						//Triage score calculation
						IF "c1"
							CharacterGetStat(_Vitality, _Char, Vitality)
						THEN
							Multiply(_Vitality, -1.0)
							Add(_Vitality, 1.0)
							Multiply(_Vitality, 5.0)
							Add(_Score, _Vitality)
						ENDIF
						
						//Status calculation
						IF "c1"
							CharacterHasStatus(_Char, STUNNED)
						THEN
							Add(_Score, 5.0)
						ENDIF
						IF "c1"
							CharacterHasStatus(_Char, CONSUME, "AMER_STATUS_Shocked")
						THEN
							Add(_Score, 3.0)
						ENDIF
						IF "c1"
							CharacterHasStatus(_Char, WET)
						THEN
							Add(_Score, 1.0)
						ENDIF
						
						//Surface calculation
						IF "c1|c2|c3"
							IsInSurface(_Char, Electrified)
							IsInSurface(_Char, BloodElectrified)
							IsInSurface(_Char, CloudStatic)
						THEN
							Add(_Score, 3.0)
						ENDIF
						IF "c1|c2|c3"
							IsInSurface(_Char, Water)
							IsInSurface(_Char, Blood)
							IsInSurface(_Char, CloudSteam)
						THEN
							Add(_Score, 1.0)
						ENDIF
						
						//Worth more if target hasn't taken their turn yet.
						IF "c1"
							CharacterGetStat(_TargetInit, _Char, Initiative)
						THEN
							IF "c1"
								IsLessThen(_TargetInit, %Target_ImmuneToElectrifying_MyInitiative)
							THEN
								Add(_Score, 1.0)
							ENDIF
						ENDIF
						
						// Range score calculation
						IF "c1&c2"
							CharacterGetSkillRange(_SkillRangeMin, _SkillRangeMax, __Me, Target_ImmuneToElectrifying)
							GetDistance(_DistanceToChar, __Me, _Char)
						THEN
							Set(_DistanceScore, _DistanceToChar)
							IF "c1"
								IsGreaterThen(_DistanceScore, _SkillRangeMax)
							THEN
								Subtract(_DistanceScore, _SkillRangeMax)
								Multiply(_DistanceScore, -1.0)
							ELIF "c1"
								IsLessThen(_DistanceScore, _SkillRangeMin)
							THEN
								Subtract(_DistanceScore, _SkillRangeMin)
								Multiply(_DistanceScore, 1.0)
							ELSE 
								Set(_DistanceScore, 0.0)
							ENDIF
							Add(_Score, _DistanceScore)
						ENDIF
						
						// Can see score calculation
						IF "!c1"
							CharacterCanSee(__Me, _Char)
						THEN
							Add(_Score, -10.0)
						ENDIF
						
						IF "c1"
							IsGreaterThen(_Score, %SkillTargetScore_Target_ImmuneToElectrifying)
						THEN
							Set(%SkillTargetChar_Target_ImmuneToElectrifying, _Char)
							Set(%SkillTargetScore_Target_ImmuneToElectrifying, _Score)
						ENDIF
					ENDIF
				ENDIF
			ENDIF
		ENDIF
	ENDIF
	// [~CALC_CHAR_TARGET_ACTIONS]
	
	
	
	
	
	
	
	