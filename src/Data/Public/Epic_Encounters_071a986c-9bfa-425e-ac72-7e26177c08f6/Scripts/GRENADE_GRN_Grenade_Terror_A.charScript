#TEMPLATE "_SKILLFROMITEM_Base" : GRN_Grenade_Terror_A, null

INIT
CHARACTER:__Me
FLOAT:%SkillTargetScore_GRN_Grenade_Terror_A=-1000000
// [GLOBAL_VARS]
FLOAT:%GRN_Grenade_Terror_A_SplashScore = 0.0
// [~GLOBAL_VARS]

EVENTS

// [EVENTS]
// [~EVENTS]

// Calculate the priority of the skill
EVENT CalculateScore_GRN_Grenade_Terror_A
VARS
	// [CALC_VARS]
	FLOAT:_SkillRangeMin
	FLOAT:_SkillRangeMax
	FLOAT:_SkillImpactRange
	FLOAT3:_TargetPos
	// [~CALC_VARS]
ON
	OnFunction("CalculateScore_GRN_Grenade_Terror_A")
ACTIONS
	//Don't bother using Grenade if it's a bad target. So set
	//score to zero here instead of -million.
	Set(%SkillTargetScore_GRN_Grenade_Terror_A, 0.0)

	// [CALC_ACTIONS_PRE]
	// [~CALC_ACTIONS_PRE]
	// [CALC_ACTIONS]
	IF "c1&c2"
	    CharacterGetSkillRange(_SkillRangeMin, _SkillRangeMax, __Me, %SkillID_GRN_Grenade_Terror_A)
	    CharacterGetSkillImpactRange(_SkillImpactRange, __Me, %SkillID_GRN_Grenade_Terror_A)
	THEN
		IF "c1"
			FindPosition(_TargetPos, __Me, _SkillRangeMax, _SkillImpactRange, __Me, -1, 0, 4, -1)
		THEN
		    Set(%SkillTargetPos_GRN_Grenade_Terror_A, _TargetPos)
		    Add(%ActionScore_GRN_Grenade_Terror_A, 20.0)
		ELIF "c1"
			FindPosition(_TargetPos, __Me, _SkillRangeMax, _SkillImpactRange, __Me, -1, 0, 3, -1)
		THEN
		    Set(%SkillTargetPos_GRN_Grenade_Terror_A, _TargetPos)
		    Add(%ActionScore_GRN_Grenade_Terror_A, 20.0)
		ELIF "c1"
			FindPosition(_TargetPos, __Me, _SkillRangeMax, _SkillImpactRange, __Me, -1, 0, 2, -1)
		THEN
		    Set(%SkillTargetPos_GRN_Grenade_Terror_A, _TargetPos)
		    Add(%ActionScore_GRN_Grenade_Terror_A, 20.0)
		ELSE
		    IterateCharactersInCombat(__Me, "CalculateTarget_GRN_Grenade_Terror_A", Lowest, Vitality)
		    IF "!c1&c2"
				IsEqual(%SkillTargetChar_GRN_Grenade_Terror_A, null)
				IsGreaterThen(%SkillTargetScore_GRN_Grenade_Terror_A, 0.0)
		    THEN
				Add(%ActionScore_GRN_Grenade_Terror_A, %SkillTargetScore_GRN_Grenade_Terror_A)
		    ENDIF
		ENDIF
	ENDIF
	// [~CALC_ACTIONS]
	
	
	
EVENT SplashScore_GRN_Grenade_Terror_A
VARS
	ABILITY:_SplashSaveType = Willpower
	CHARACTER:_SplashChar
	INT:_SplashSaveAmount
	FLOAT:_SplashScore
ON
	OnIterateCharacter(_SplashChar, "SplashScore_GRN_Grenade_Terror_A")
ACTIONS
	IF "!c1"
		CharacterIsDead(_SplashChar)
	THEN
		Set(_SplashScore, 0.0)
		IF "c1"
			CharacterGetAbility(_SplashSaveAmount, _SplashChar, _SplashSaveType)
		THEN
			//Use Saving throw as a score modifier.
			Cast(_SplashScore, _SplashSaveAmount)
			Multiply(_SplashScore, -0.10)
			Add(_SplashScore, 1.0)
			IF "c1|c2|c3|c4|c5"
				IsLessThen(_SplashScore, 0.0)
				CharacterHasTalent(_SplashChar, Courageous)
				CharacterHasStatus(_SplashChar, RAGED)
				CharacterHasStatus(_SplashChar, CONSUME, SKILLBOOST_Leadership5)
				CharacterHasStatus(_SplashChar, CONSUME, SKILLBOOST_Leadership6)
			THEN
				Set(_SplashScore, 0.0)
			ENDIF
		ENDIF
		
		//If it is an enemy.
		IF "(!c1&!c2)|(c3&c4)"
			CharacterIsAlly(__Me, _SplashChar)
			CharacterHasStatus(_SplashChar CHARMED)
			CharacterIsAlly(__Me, _SplashChar)
			CharacterHasStatus(_SplashChar CHARMED)
		THEN
			IF "c1"
				CharacterIsSummon(_SplashChar)
			THEN
				IF "!c1"
					CharacterHasStatus(_SplashChar CHARMED)
				THEN
					Add(%GRN_Grenade_Terror_A_SplashScore, _SplashScore)
				ENDIF
			ELSE
				Multiply(_SplashScore, 5.0)
				Add(%GRN_Grenade_Terror_A_SplashScore, _SplashScore)
			ENDIF
		//If it is an ally.
		ELIF "!c1"
			CharacterIsSummon(_SplashChar)
		THEN
			Multiply(_SplashScore, -4.0)
			Add(%GRN_Grenade_Terror_A_SplashScore, _SplashScore)
		ENDIF
	ENDIF

	
	
// Calculate the character we should attack
EVENT CalculateTarget_GRN_Grenade_Terror_A
VARS	
	CHARACTER:_Char
	// [CALC_TARGET_VARS]
	FLOAT:_Score
	FLOAT:_HostileScore=0
	FLOAT:_SkillRangeMin
	FLOAT:_SkillRangeMax
	FLOAT:_DistanceToChar
	FLOAT:_DistanceScore
	FLOAT:_Vitality
	FLOAT:_Radius
	FLOAT:_SaveScore
	INT:_SaveAmount
	ABILITY:_SaveType = Willpower
	// [~CALC_TARGET_VARS]
ON
	OnIterateCharacter(_Char, "CalculateTarget_GRN_Grenade_Terror_A")	
ACTIONS
	// [CALC_CHAR_TARGET_ACTIONS_PRE]
	Set(_Score, 0.0)
	// [~CALC_CHAR_TARGET_ACTIONS_PRE]
	// [CALC_CHAR_TARGET_ACTIONS]
	IF "c1"
	    CharacterIsEnemy(__Me, _Char)
	THEN
		IF "!c1"
			CharacterIsDead(_Char)
		THEN
			IF "!c1&!c2"
			    CharacterHasStatus(_Char, INVISIBLE)
			    CharacterHasStatus(_Char, SNEAKING)
			THEN
				IF "!c1"
					CharacterHasStatus(_Char, CHARMED)
				THEN
					IF "!c1&!c2&!c3&!c4"
						CharacterHasTalent(_Char, Courageous
						CharacterHasStatus(_Char, RAGED)
						CharacterHasStatus(_Char, CONSUME, SKILLBOOST_Leadership5)
						CharacterHasStatus(_Char, CONSUME, SKILLBOOST_Leadership6)
					THEN
						IF "!c1"
							CharacterHasStatus(_Char, INVULNERABLE)
						THEN
							//Kill range calculation
							IF "c1"
								CharacterGetStat(_Vitality,  _Char, Vitality)
							THEN
								//Favor high vitality targets.
								Multiply(_Vitality, 5.0)
								Add(_Score, _Vitality)
							ENDIF
							
							//Marked.
							IF "c1"
								CharacterHasStatus(_Char, AGGRO_MARKED)
							THEN
								Add(_Score, 1.0)
							ENDIF
							
							//Splash score calc.
							IF "c1"
								CharacterGetSkillImpactRange(_Radius, __Me, %SkillID_GRN_Grenade_Terror_A)
							THEN
								Set(%GRN_Grenade_Terror_A_SplashScore, 0.0)
								IterateCharactersNear(_Char, _Radius, "SplashScore_GRN_Grenade_Terror_A")
								Add(_Score, %GRN_Grenade_Terror_A_SplashScore)
							ENDIF
									
							//Saving Throw score calculation
							IF "c1"
								CharacterGetAbility(_SaveAmount, _Char, _SaveType)
							THEN
								Cast(_SaveScore, _SaveAmount)
								Multiply(_SaveScore, -2.0)
								Add(_Score, _SaveScore)
							ENDIF
								
							//Summons are worth less.
							IF "c1"
								CharacterIsSummon(_Char)
							THEN
								Add(_Score, -5.0)
							ENDIF
							
							// Range score calculation
							IF "c1&c2"
								CharacterGetSkillRange(_SkillRangeMin, _SkillRangeMax, __Me, %SkillID_GRN_Grenade_Terror_A)
								GetDistance(_DistanceToChar, __Me, _Char)
							THEN
								Set(_DistanceScore, _DistanceToChar)
								IF "c1"
									IsGreaterThen(_DistanceScore, _SkillRangeMax)
								THEN
									Subtract(_DistanceScore, _SkillRangeMax)
									Multiply(_DistanceScore, -1.0)
								ELIF "c1"
									IsLessThen(_DistanceScore, _SkillRangeMin)
								THEN
									Subtract(_DistanceScore, _SkillRangeMin)
									Multiply(_DistanceScore, 1.0)
								ELSE 
									Set(_DistanceScore, 0.0)
								ENDIF
								Add(_Score, _DistanceScore)
							ENDIF
							
							// Can see score calculation
							IF "!c1"
								CharacterCanSee(__Me, _Char)
							THEN
								Add(_Score, -10.0)
							ENDIF
							
							IF "c1"
								IsGreaterThen(_Score, %SkillTargetScore_GRN_Grenade_Terror_A)
							THEN
								Set(%SkillTargetChar_GRN_Grenade_Terror_A, _Char)
								Set(%SkillTargetScore_GRN_Grenade_Terror_A, _Score)
							ENDIF
						ENDIF
					ENDIF
				ENDIF
			ENDIF
		ENDIF
	ENDIF
	// [~CALC_CHAR_TARGET_ACTIONS]
	
	
	
	
	
	
	
	
	