INIT
ITEM:__Me
INT:%CurrentIsVisible=0
INT:%NewIsVisible=0
INT:%TimerStopped=0
EXTERN FLOAT:%MinPerception=0
EXTERN FLOAT:%MinDistance=0
EXTERN FLOAT:%MinPerceptionHardcore= 0
EXTERN FLOAT:%MinPerceptionCasual= 0
EXTERN FLOAT:%AMER_RequiredPerceptionModifier = -2
EXTERN INT:%IsTrap = 1

/****************************************************************************
*                       About Casual and Hardocre							*
* 																			*
*  By default, Hardcore increases value by 2 and Casul decreases it by 2. 	*
*  If you wish to keep the default, just leave 0 in the Hardocre and      	*
*   casual field.                                                         	*
*  If you fill in a custom hard value in the Hardocore or Casual field,   	*
*   the script will use that value instead.								 	*
*  /!\ For story purposes, a value of 5 in Normal doesn't great increases 	*
*   in Hardcore. /!\    													*
* 																			*
*  Exemples:																*
*  If you put Normal:7, Hardcore:0, Casual: 6, the values in game will be:	*
*    N:7, H:9, C:6															*
*  If you put Normal:5, Hardocre:0, Casual:0, the values will be:			*
* 	 N:5, H:5, C:5															*
*																			*
*																			*
*				Modified by Matteo "Ameranth" T, Jan, 2017.					*
*			(efficiency increase, perception requirement reduction)			*
*																			*
****************************************************************************/

EVENTS

EVENT InitializeHidden
ON
	OnInit()
ACTIONS
	IF "c1&c2"
		IsEqual(%MinPerceptionHardcore, 0)
		IsEqual(%MinPerception, 5)
	THEN
		Set(%MinPerceptionHardcore, 5)
	ELIF "c1"
		IsEqual(%MinPerceptionHardcore, 0)
	THEN
		Set(%MinPerceptionHardcore, %MinPerception)
		Add(%MinPerceptionHardcore, 2)
	ENDIF
	IF "c1"
		IsEqual(%MinPerceptionCasual, 0)
	THEN
		Set(%MinPerceptionCasual, %MinPerception)
		Subtract(%MinPerceptionCasual, 2)
		IF "c1"
			IsLessThen(%MinPerceptionCasual, 5)
		THEN
			Set(%MinPerceptionCasual, 5)
		ENDIF
	ENDIF
	IF "c1"
		IsEqual(%CurrentIsVisible,0)
	THEN
		StartTimer("UpdateHidden",0.2,-1)
	ENDIF
	SetVisible(__Me,%CurrentIsVisible)
	
EVENT ShutdownHidden
ON
	OnShutdown()
ACTIONS
	StopTimer("UpdateHidden")

EVENT UpdateHidden
ON
	OnTimer("UpdateHidden")
ACTIONS
	Set(%NewIsVisible,0)
	IterateCharactersNear(__Me, %MinDistance, "HiddenObjectCheck")
	//IterateParty("HiddenObjectCheck")
	IF "!c1&c2"
		IsEqual(%CurrentIsVisible,%NewIsVisible)
		IsEqual(%TimerStopped,0)
	THEN
		Set(%CurrentIsVisible,%NewIsVisible)
		SetVisible(__Me,%CurrentIsVisible)
	ENDIF
	
	IF "c1"
		IsEqual(%CurrentIsVisible,1)
	THEN
		StopTimer("UpdateHidden")
	ENDIF

EVENT CheckHidden
VARS
CHARACTER:_Char
FLOAT:_Perception
FLOAT:_PercToSee
FLOAT:_Dist
STRING:_Event
ON
	OnIterateCharacter(_Char,"HiddenObjectCheck")
ACTIONS
	IF "c1"
		IsEqual(%NewIsVisible,0)
	THEN
		IF "c1"
			IsEqual(%TimerStopped,0)
		THEN
			IF "c1"
				CharacterIsPlayer(_Char)
			THEN
				IF "!c1"
					CharacterIsDead(_Char)
				THEN
					IF "c1"
						CharacterGetStat(_Perception, _Char, TrapPerception)
					THEN
						IF "c1"
							IsCasual()
						THEN
							Set(_PercToSee, %MinPerceptionCasual)
						ELIF "c1"
							IsHardcore()
						THEN
							Set(_PercToSee, %MinPerceptionHardcore)
						ELSE
							Set(_PercToSee, %MinPerception)
						ENDIF
							
						Add(_PercToSee, %AMER_RequiredPerceptionModifier)
						
						IF "!c1"
							IsLessThen(_Perception, _PercToSee)
						THEN
							Set(%NewIsVisible,1)
							IF "c1"
								IsEqual(%IsTrap, 0)
							THEN
								SetMaterial(__Me,"39ae3071-a9bd-42cf-9291-82df3c272d6e",3,1,1,1,1,1,0)
								PlaySound(__Me, "Reveal_Rumble")
								GetRandom(_Event, "HiddenPerceptionReveal", "HiddenPerceptionReveal2", "HiddenPerceptionReveal3", "HiddenPerceptionReveal4")
								CharacterEvent(_Char,_Event)
							ELSE
								GetRandom(_Event, "HiddenTrapReveal", "HiddenTrapReveal2", "HiddenTrapReveal3", "HiddenTrapReveal4")
								CharacterEvent(_Char,_Event)
							ENDIF
						ENDIF
					ENDIF
				ENDIF
			ENDIF
		ENDIF
	ENDIF
		/*
		//Oh my holy what my wow this code someone please hire me.
	IF "!c8&!c9&c1&!c2&c3&c4&!c5&c6&c7" 
		IsEqual(%NewIsVisible,0)
		CharacterIsDead(_Char)
		CharacterGetStat(_Perception,_Char,TrapPerception)
		GetDistance(_Dist,_Char,__Me)
		IsLessThen(_Perception,%MinPerception)
		IsLessThen(_Dist,%MinDistance)
		IsEqual(%TimerStopped,0)
		IsCasual()
		IsHardcore()
	THEN
		Set(%NewIsVisible,1)
		IF "c1"
			IsEqual(%IsTrap, 0)
		THEN
			SetMaterial(__Me,"39ae3071-a9bd-42cf-9291-82df3c272d6e",3,1,1,1,1,1,0)
			PlaySound(__Me, "Reveal_Rumble")
			GetRandom(_Event, "HiddenPerceptionReveal", "HiddenPerceptionReveal2", "HiddenPerceptionReveal3", "HiddenPerceptionReveal4")
			CharacterEvent(_Char,_Event)
		ELSE
			GetRandom(_Event, "HiddenTrapReveal", "HiddenTrapReveal2", "HiddenTrapReveal3", "HiddenTrapReveal4")
			CharacterEvent(_Char,_Event)
		ENDIF
	ELIF "c8&c1&!c2&c3&c4&!c5&c6&c7" 
		IsEqual(%NewIsVisible,0)
		CharacterIsDead(_Char)
		CharacterGetStat(_Perception,_Char,TrapPerception)
		GetDistance(_Dist,_Char,__Me)
		IsLessThen(_Perception,%MinPerceptionCasual)
		IsLessThen(_Dist,%MinDistance)
		IsEqual(%TimerStopped,0)
		IsCasual()
	THEN
		Set(%NewIsVisible,1)
		IF "c1"
			IsEqual(%IsTrap, 0)
		THEN
			SetMaterial(__Me,"39ae3071-a9bd-42cf-9291-82df3c272d6e",3,1,1,1,1,1,0)
			GetRandom(_Event, "HiddenPerceptionReveal", "HiddenPerceptionReveal2", "HiddenPerceptionReveal3", "HiddenPerceptionReveal4")
			CharacterEvent(_Char,_Event)
		ELSE
			GetRandom(_Event, "HiddenTrapReveal", "HiddenTrapReveal2", "HiddenTrapReveal3", "HiddenTrapReveal4")
			CharacterEvent(_Char,_Event)
		ENDIF
	ELIF "c8&c1&!c2&c3&c4&!c5&c6&c7" 
		IsEqual(%NewIsVisible,0)
		CharacterIsDead(_Char)
		CharacterGetStat(_Perception,_Char,TrapPerception)
		GetDistance(_Dist,_Char,__Me)
		IsLessThen(_Perception,%MinPerceptionHardcore)
		IsLessThen(_Dist,%MinDistance)
		IsEqual(%TimerStopped,0)
		IsHardcore()
	THEN
		Set(%NewIsVisible,1)
		IF "c1"
			IsEqual(%IsTrap, 0)
		THEN
			SetMaterial(__Me,"39ae3071-a9bd-42cf-9291-82df3c272d6e",3,1,1,1,1,1,0)
			GetRandom(_Event, "HiddenPerceptionReveal", "HiddenPerceptionReveal2", "HiddenPerceptionReveal3", "HiddenPerceptionReveal4")
			CharacterEvent(_Char,_Event)
		ELSE
			GetRandom(_Event, "HiddenTrapReveal", "HiddenTrapReveal2", "HiddenTrapReveal3", "HiddenTrapReveal4")
			CharacterEvent(_Char,_Event)
		ENDIF
	ENDIF
	*/
	
EVENT StoryReveal
ON
	OnItemEvent(__Me,"StoryReveal")
ACTIONS
	Set(%CurrentIsVisible,1)
	Set(%NewIsVisible,1)
	Set(%TimerStopped,1)
	SetVisible(__Me,1)
	StopTimer("UpdateHidden")
